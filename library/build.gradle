apply plugin: "com.android.library"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 25
        version "1.0"
        versionCode 1
    }
}

ext {
    bintrayRepo = "maven"
    bintrayName = "number-progress-bar"

    publishedGroupId = "com.dona278.numberprogressbar"
    libraryName = "NumberProgressBar"
    artifact = "com.dona278.numberprogressbar"

    libraryDescription = "A beautiful, slim Android ProgressBar based on original project daimajia/NumberProgressBar"

    siteUrl = "https://github.com/Dona278/NumberProgressBar"
    gitUrl = "https://github.com/Dona278/NumberProgressBar.git"

    libraryVersion = "1.0"

    developerId = "dona278"
    developerName = "Davide Donadello"
    developerEmail = "d.donadels@gmail.com"

    licenseName = "The MIT License (MIT)"
    licenseUrl = "https://opensource.org/licenses/MIT"
    allLicenses = ["MIT"]
}

apply plugin: "com.github.dcendents.android-maven"

// Maven Group ID for the artifact
group = publishedGroupId

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging "aar"
                groupId publishedGroupId
                artifactId artifact

                // Add your description here
                name libraryName
                description libraryDescription
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name licenseName
                        url licenseUrl
                    }
                }
                developers {
                    developer {
                        id developerId
                        name developerName
                        email developerEmail
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

apply plugin: 'com.jfrog.bintray'

version = libraryVersion

// Android libraries
if (project.hasProperty("android")) {
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

// Bintray
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
            gpg {
                //Determines whether to GPG sign the files. The default is false
                sign = true
                //Optional. The passphrase for GPG signing'
                passphrase = properties.getProperty("bintray.gpg.password")
            }
        }
    }
}